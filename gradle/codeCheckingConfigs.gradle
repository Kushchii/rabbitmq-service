checkstyle {
    toolVersion = checkstyleVersion
    configFile = file("$projectDir/gradle/config/checkstyle.xml")
    showViolations = true
    ignoreFailures = false
    maxErrors = 0
}

def coverageProperty = 'coverageLimit'
def jacocoMinCoverage = project.hasProperty(coverageProperty) ? project.getProperty(coverageProperty) : '0.7'
jacoco {
    toolVersion = jacocoVersion
}

test {
    finalizedBy jacocoTestReport // report is always generated after tests run
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
    reports {
        xml.required = true
        csv.required = false
        html.required = true
    }
    finalizedBy jacocoTestCoverageVerification
}

jacocoTestCoverageVerification {
    violationRules {
        failOnViolation = false
        rule {
            element = 'BUNDLE'
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = jacocoMinCoverage as BigDecimal
            }
        }
    }
}
